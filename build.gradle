buildscript {
  ext.kotlinVersion = '2.0.0'
  repositories {
    google()
    mavenCentral()
    maven { url 'https://jitpack.io' }
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:8.13.0'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
  }
}

allprojects {
  repositories {
    google()
    mavenCentral()
    maven { url 'https://jitpack.io' }
  }
}

subprojects { project ->
  group = GROUP
  version = VERSION_NAME

  apply plugin: 'checkstyle'

  task checkstyle(type: Checkstyle) {
    configFile rootProject.file('checkstyle.xml')
    source 'src/main/java'
    ignoreFailures false
    showViolations true
    include '**/*.java'

    classpath = files()
  }

  afterEvaluate {
    if (project.tasks.findByName('check')) {
      check.dependsOn('checkstyle')
    }
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

ext {
  minSdkVersion = 21
  targetSdkVersion = 35
  compileSdkVersion = 35
  buildToolsVersion = '35.0.0'
  sourceCompatibilityVersion = JavaVersion.VERSION_17
  targetCompatibilityVersion = JavaVersion.VERSION_17
}

ext.deps = [
    gson          : 'com.google.code.gson:gson:2.8.2',
    conceal       : 'com.github.triniwiz:conceal:2d77782ecf',

    // Test dependencies
    junit         : 'junit:junit:4.12',
    truth         : 'com.google.truth:truth:0.28',
    robolectric   : 'org.robolectric:robolectric:3.3',
    mockito       : 'org.mockito:mockito-core:2.8.9',
    espressoRunner: 'com.android.support.test:runner:0.5',
    espressoRules : 'com.android.support.test:rules:0.5',
    kotlin        : "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

]
